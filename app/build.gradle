import transform.MyTransform
import transform.methodtime.MethodTimeTransformPlugin
import transform.life.LifeTransformPlugin
import transform.hello.HelloTransformPlugin

//import com.xfhy.plugin.CustomPlugin
//import com.xfhy.plugin.MyAndroidPlugin
//import com.xfhy.plugin.HookAssetsPlugin
import com.xfhy.plugin.ManifestDemoPlugin

//build.gradle对应的是Project对象,plugins是里面一个方法,设置插件用的
//在模块的build.gradle中可以通过rootProject获取root build.gradle所对应的对象.

plugins {

}


apply plugin: 'com.android.application'
//apply plugin: CustomPlugin
//apply plugin: MyAndroidPlugin
//apply plugin: HookAssetsPlugin
//apply plugin: ManifestDemoPlugin
//apply plugin: MethodTimeTransformPlugin
//apply plugin: LifeTransformPlugin
apply plugin: HelloTransformPlugin
//apply from: 'file.gradle'
//apply from: 'task.gradle'
//apply from: 'net.gradle'

//testExtension {
//    compileSdkVersion rootProject.ext.compileSdkVersion
//    buildToolsVersion rootProject.ext.buildToolsVersion
//    applicationId 'com.xfhy.gradledemo'
//    minSdkVersion '22'
//}

//android其实是插件里面的dsl
android {

    //在项目中使用Transform
    //registerTransform(new MyTransform())

    compileSdkVersion Config.compileSdkVersion
    buildToolsVersion Config.buildToolsVersion
    defaultConfig {
        applicationId Config.applicationId
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName

        buildConfigField("String[]", "TAB", "{\"首页\",\"排行榜\",\"我的\"}")
        buildConfigField("String", "APP_DESCRIPTION", "\"你没有见过的船新版本\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
        free {
            java.srcDirs = ['src/free/java']
        }

        svip {
            java.srcDirs = ['src/common/java']
        }

        vip {
            java.srcDirs = ['src/common/java']
        }
    }

    signingConfigs {
        debug {
            storeFile file('../keys/xfhy.jks')
            storePassword "qqqqqq"
            keyAlias "xfhy"
            keyPassword "qqqqqq"
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            storeFile file('../keys/xfhy.jks')
            storePassword "qqqqqq"
            keyAlias "xfhy"
            keyPassword "qqqqqq"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    flavorDimensions "channel"
    productFlavors {
        free {
            dimension "channel"
            //程序包名
            applicationId "com.xfhy.free"
            //替换清单文件中的标签
            manifestPlaceholders = [
                    APP_ICON: "@drawable/ic_launcher",
                    APP_NAME: "xx免费版",
            ]
            //versionName
            versionName "2.0.0"
            //versionCode
            versionCode 2
        }
        vip {
            dimension "channel"
            //程序包名
            applicationId "com.xfhy.vip"
            //替换清单文件中的标签
            manifestPlaceholders = [
                    APP_ICON: "@drawable/ic_launcher",
                    APP_NAME: "xxVip版",
            ]
            //versionName
            versionName "3.0.0"
            //versionCode
            versionCode 3
        }
        svip {
            dimension "channel"
        }
    }

    //返回应用程序项目包含的构建变体的集合
    applicationVariants.all { variant ->

        //构建变体专属变量
        switch (variant.flavorName) {
            case 'free':
                buildConfigField("String", "BASE_URL", "\"http://31.13.66.23\"")
                buildConfigField("String", "TOKEN", "\"dhaskufguakfaskfkjasjhbfree\"")
                break
            case 'vip':
                buildConfigField("String", "BASE_URL", "\"http://31.13.66.24\"")
                buildConfigField("String", "TOKEN", "\"dhaskfagafkjasjhbvip\"")
                break
            case 'svip':
                buildConfigField("String", "BASE_URL", "\"http://31.13.66.25\"")
                buildConfigField("String", "TOKEN", "\"dhaskufgufgsdagajasjhbsvip\"")
                break
        }

        //打包文件命名
        variant.outputs.all {
            def type = variant.buildType.name
            def channel = variant.flavorName
            //eg: demo_2.0.0_free_debug
            outputFileName = "demo_${variant.versionName}_${channel}_${type}.apk"
            //println("name :${variant.name} , baseName:${variant.baseName}")   输出eg: name :freeRelease , baseName:free-release
        }

        //这里可以通过variant.taskName 来获取相应的task,这里的taskName是task的名字
        //示范: 获取检查清单文件的task
        //def task = variant.checkManifest
        //def task = variant.getCheckManifestProvider()
        //println(task.name)

    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation Config.depConfig.support.recyclerview_androidx

    // implementation 依赖会出现在编译产物中.但是最终产物中的该依赖不会向外暴露接口,这种方式可以有效减少recompile时所需要编译的模块,提高编译速度.
    // api 和上面一样,但是在编译期会传递依赖给外部
    // compileOnly 仅编译期有效,不会出现在最终产物中
    // runtimeOnly 仅运行期有效,会出现在编译产物中
    // annotationProcessor 注解处理器依赖(包含 META-INF/services/javax.annotation.processing.Processor 的 jar 包)
}

//项目属性,可在运行时加入参数 eg:  gradle clean -Ppwd="123"
if (hasProperty("pwd")) {
    //简单判断一下是否有这个参数,如果没有,直接通过property取会报错(Could not get unknown property 'pwd' for project ':app' of type org.gradle.api.Project.)
    println("property test 卧槽------------ ${property("pwd")}")
}